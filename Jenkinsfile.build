dockerImage = 'dockernexus101/app01'

def readCurrentTag() {
	def tag = sh(returnStdout: true, script: "git --no-pager tag --points-at HEAD").trim()
	return tag
}

pipeline {
	agent { label 'arm64' }
	options {
		parallelsAlwaysFailFast()
	}
	environment {
		TAG_NAME = readCurrentTag()
	}
    stages {
		stage('Prepare variables') {
			steps {
				script {
					if (buildingTag()) {
						echo "TAG_NAME is not null: ${env.TAG_NAME}"
					}
					imgTag = ''
					if (env.BRANCH_NAME == 'main') {
						// check if the branch is main
						commit = env.GIT_COMMIT.take(7)
						imgTag = "main-${commit}"
					} else if (env.BRANCH_NAME != null) {
						branchName = env.BRANCH_NAME.replaceAll(/[^a-zA-Z0-9]+/, "")
						commit = env.GIT_COMMIT.take(7)
						imgTag = "build-${branchName}-${commit}"
					} else {
						imgTag = readCurrentTag()
						env.TAG_NAME = imgTag
					}
				}
			}
		}
        stage('Build Docker Image') {
			parallel {
				stage('Build Docker Image amd64') {
					agent { label 'amd64' }
					steps {
						script {
							imageamd64 = docker.build("${dockerImage}:${imgTag}-amd64")
						}
					}
				}
				stage('Build Docker Image arm64') {
					agent { label 'arm64' }
					steps {
						script {
							imagearm64 = docker.build("${dockerImage}:${imgTag}-arm64")
						}
					}
				}
			}
		}
		
		stage('Push Docker Image') {
			parallel {
				stage('Push Docker Image amd64') {
					agent { label 'amd64' }
					steps {
						script {
							withDockerRegistry(credentialsId: 'dockernexus101') {
								imageamd64.push()
							}
						}
					}
				}
				stage('Push Docker Image arm64') {
					agent { label 'arm64' }
					steps {
						script {
							withDockerRegistry(credentialsId: 'dockernexus101') {
								imagearm64.push()
							}
						}
					}
				}
			}
        }

        stage('Build Docker manifest and push') {
            steps {
				script {
					withDockerRegistry(credentialsId: 'dockernexus101') {
						sh """
							docker manifest create ${dockerImage}:${imgTag} ${dockerImage}:${imgTag}-amd64 ${dockerImage}:${imgTag}-arm64
							docker manifest push ${dockerImage}:${imgTag}
						"""
					}
				}
            }
        }

		stage('Trigger Deployment') {
			when {
				anyOf {
					branch 'main'
					expression {
						return readCurrentTag()
					}
				}
			}
			steps {
				script {
					environment = 'dev'
					if (readCurrentTag()) {
						echo 'deploying to prod'
						environment = 'prod'
					}
					build job: 'ST2DCE_Project_Nexus/Deploy', parameters: [
						string(name: 'env', value: environment),
						string(name: 'version', value: imgTag)
					],
					wait: false
				}
			}
		}
    }
}
